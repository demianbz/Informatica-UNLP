{2- Realice un programa en el cual 2 robots corren una carrera. El recorrido realizado por cada uno es el que se muestra en la siguiente figura. Durante el recorrido el robot 1 debe juntar todas las flores que encuentre en los vértices de cada escalón, y el robot 2 debe juntar todos los papeles que encuentre en los vértices de cada escalón. Al finalizar deben informar la cantidad de elementos recogidos. El robot 1 debe iniciar su recorrido en la esquina (1,1) y el robot 2 en la esquina (31,1). Al finalizar la carrera, un robot jefe (inicializado en la esquina (15,1) ) debe informar qué robot llegó primero a la esquina central de su recorrido.}

programa ejercicio2
procesos
  proceso juntarFlores (ES flores:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:=flores + 1
  fin
  
  proceso juntarPapeles (ES papeles:numero)
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
      papeles:=papeles + 1
  fin
  
  proceso escalonF (ES flores:numero ; E n:numero)
  comenzar 
    repetir n
      mover
    juntarFlores(flores)
    derecha
    repetir n
      mover
    juntarFlores(flores)
    repetir 3
      derecha
  fin
  
  proceso escalonP (ES papeles:numero ; E n:numero)
  comenzar 
    repetir n
      mover
    juntarPapeles(papeles)
    repetir 3
      derecha
    repetir n
      mover
    juntarPapeles(papeles)
    derecha
  fin
  
  proceso EsquinaCentralF (ES flores:numero ; E quienSoy:numero)
  comenzar
    mover
    juntarFlores(flores)
    derecha
    BloquearEsquina(16,16)
    mover
    juntarFlores(flores)
    EnviarMensaje(quienSoy,rj)
    repetir 2
      derecha
    mover
    LiberarEsquina(16,16)
  fin
  
  proceso EsquinaCentralP (ES papeles:numero ; E quienSoy:numero)
  comenzar
    mover
    repetir 3
      derecha
    BloquearEsquina(16,16)
    mover
    juntarPapeles(papeles)
    EnviarMensaje(quienSoy,rj)    
    repetir 2
      derecha
    mover
    LiberarEsquina(16,16)
  fin
  
areas
  ciudad: AreaC(1,1,100,100)

robots
  robot florero
  variables
    flores:numero
    n:numero
    quienSoy:numero
    termine:numero    
  comenzar
    RecibirMensaje(quienSoy,rj)
    flores:=0
    termine:=0
    n:=5
    repetir 4
      escalonF(flores,n)
      n:=n-1
    EsquinaCentralF(flores,quienSoy)
    n:=2
    repetir 4
      escalonF(flores,n)
      n:=n+1
    EnviarMensaje(termine,rj)
    Informar('Cant Flores ',flores)
  fin
  
  robot papelero
  variables
    papeles:numero
    n:numero
    quienSoy:numero
    termine:numero
  comenzar
    RecibirMensaje(quienSoy,rj)
    papeles:=0
    termine:=0
    n:=5
    repetir 4
      escalonP(papeles,n)
      n:=n-1
    EsquinaCentralP(papeles,quienSoy)
    n:=2
    repetir 4
      escalonP(papeles,n)
      n:=n+1
    EnviarMensaje(termine,rj)
    Informar('Cant Papeles ',papeles)
  fin
  
  robot jefe
  variables
    idGanador: numero
    id: numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    RecibirMensaje(idGanador,*)
    repetir 3 {Los otros 3 mensajes que no me importan el unico que me importa es el primer}
      RecibirMensaje(id, *)
    Informar('RobotGanador ', idGanador)
  fin
     
variables
  r1:florero
  r2:papelero
  rj:jefe
comenzar
  AsignarArea(r1,ciudad)
  AsignarArea(r2,ciudad)  
  AsignarArea(rj,ciudad)
  Iniciar(rj,15,1)
  Iniciar(r1,1,1)
  Iniciar(r2,31,1)
fin